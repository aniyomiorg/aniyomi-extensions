apply plugin: 'org.jmailen.kotlinter'

android {
    compileSdkVersion Config.compileSdk
    buildToolsVersion Config.buildTools

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
        release {
            manifest.srcFile "AndroidManifest.xml"
        }
        debug {
            manifest.srcFile "AndroidManifest.xml"
        }
    }

    defaultConfig {
        minSdkVersion Config.minSdk
        targetSdkVersion Config.targetSdk
        applicationIdSuffix pkgNameSuffix
        versionCode extVersionCode
        versionName "$libVersion.$extVersionCode"
        setProperty("archivesBaseName", "tachiyomi-$pkgNameSuffix-v$versionName")
        manifestPlaceholders = [
                appName : "Tachiyomi: $extName",
                extClass: extClass,
                nsfw: project.ext.properties.getOrDefault("containsNsfw", false) ? 1 : 0,
        ]
    }

    dependenciesInfo {
        includeInApk = false
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":annotations")
    implementation project(":core")

    // Lib 1.2, but using specific commit so we don't need to bump up the version
    compileOnly "com.github.tachiyomiorg:extensions-lib:a596412"

    // These are provided by the app itself
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    compileOnly 'com.github.inorichi.injekt:injekt-core:65b0440'
    compileOnly 'com.squareup.okhttp3:okhttp:3.10.0'
    compileOnly 'io.reactivex:rxjava:1.3.6'
    compileOnly 'org.jsoup:jsoup:1.10.2'
    compileOnly 'com.google.code.gson:gson:2.8.2'
    compileOnly 'com.github.salomonbrys.kotson:kotson:2.5.0'
    compileOnly project(':duktape-stub')
}

preBuild.dependsOn(lintKotlin)
lintKotlin.dependsOn(formatKotlin)
